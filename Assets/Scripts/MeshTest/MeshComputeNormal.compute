// Each #kernel tells which function to compile; you can have many kernels
#pragma kernel CSMain
#pragma kernel FixupDrawArg

//////// Constants ////////

//////// Struct Data Containers ////////

struct DrawArg
{
    uint vertexCountPerInstance;
    uint instanceCount;
    uint startVertexLocation;
    uint startInstanceLocation;
};

struct Triangle
{
    float3 normal;
    float3 color;
    // Packed verts
    int vertices[3];
};

//////// Buffers ////////

RWStructuredBuffer<int> ChunkDataBuffer;
AppendStructuredBuffer<Triangle> MeshBuffer;
RWStructuredBuffer<DrawArg> DrawArgsBuffer;
RWStructuredBuffer<uint> ChunkSize;
RWStructuredBuffer<uint> ChunkLOD;

//////// Lookup Tables ////////

static const float3 NormalDirLookup[6] =
{
    float3( 1.0f,  0.0f,  0.0f),
    float3(-1.0f,  0.0f,  0.0f),
    float3( 0.0f,  1.0f,  0.0f),
    float3( 0.0f, -1.0f,  0.0f),
    float3( 0.0f,  0.0f,  1.0f),
    float3( 0.0f,  0.0f, -1.0f)
};

static const uint3 CubeFaceDirLookup[36] =
{
    // X+
    uint3(1.0f, 0.0f, 0.0f),
    uint3(1.0f, 1.0f, 0.0f),
    uint3(1.0f, 0.0f, 1.0f),

    uint3(1.0f, 1.0f, 1.0f),
    uint3(1.0f, 0.0f, 1.0f),
    uint3(1.0f, 1.0f, 0.0f),
    
    // X-
    uint3(0.0f, 0.0f, 0.0f),
    uint3(0.0f, 0.0f, 1.0f),
    uint3(0.0f, 1.0f, 0.0f),

    uint3(0.0f, 1.0f, 1.0f),
    uint3(0.0f, 1.0f, 0.0f),
    uint3(0.0f, 0.0f, 1.0f),
    
    // Y+
    uint3(0.0f, 1.0f, 0.0f),
    uint3(0.0f, 1.0f, 1.0f),
    uint3(1.0f, 1.0f, 0.0f),

    uint3(1.0f, 1.0f, 1.0f),
    uint3(1.0f, 1.0f, 0.0f),
    uint3(0.0f, 1.0f, 1.0f),
    
    // Y-
    uint3(0.0f, 0.0f, 0.0f),
    uint3(1.0f, 0.0f, 0.0f),
    uint3(0.0f, 0.0f, 1.0f),

    uint3(1.0f, 0.0f, 1.0f),
    uint3(0.0f, 0.0f, 1.0f),
    uint3(1.0f, 0.0f, 0.0f),
    
    // Z+
    uint3(0.0f, 0.0f, 1.0f),
    uint3(1.0f, 0.0f, 1.0f),
    uint3(0.0f, 1.0f, 1.0f),

    uint3(1.0f, 1.0f, 1.0f),
    uint3(0.0f, 1.0f, 1.0f),
    uint3(1.0f, 0.0f, 1.0f),
    
    // Z-
    uint3(0.0f, 0.0f, 0.0f),
    uint3(0.0f, 1.0f, 0.0f),
    uint3(1.0f, 0.0f, 0.0f),

    uint3(1.0f, 1.0f, 0.0f),
    uint3(1.0f, 0.0f, 0.0f),
    uint3(0.0f, 1.0f, 0.0f),
};

//////// Triangle Helpers ////////

// Creates a face from 2 triangles and a direction.
void CreateCubeFace(uint dir, uint3 p, float3 col, uint lod)
{
    const float3 normal = NormalDirLookup[dir];
    const uint dirLookup = dir * 6;
    
    Triangle tri_a;
    
    const uint3 v1_a = (CubeFaceDirLookup[dirLookup]     + p) * lod;
    const uint3 v2_a = (CubeFaceDirLookup[dirLookup + 1] + p) * lod;
    const uint3 v3_a = (CubeFaceDirLookup[dirLookup + 2] + p) * lod;

    tri_a.vertices[0] = (v1_a.x & 1023) | (v1_a.y & 1023) << 10 | (v1_a.z & 1023) << 20;
    tri_a.vertices[1] = (v2_a.x & 1023) | (v2_a.y & 1023) << 10 | (v2_a.z & 1023) << 20;
    tri_a.vertices[2] = (v3_a.x & 1023) | (v3_a.y & 1023) << 10 | (v3_a.z & 1023) << 20;
    
    tri_a.normal = normal;
    tri_a.color = col;

    Triangle tri_b;

    const uint3 v1_b = (CubeFaceDirLookup[dirLookup + 3] + p)  * lod;
    const uint3 v2_b = (CubeFaceDirLookup[dirLookup + 4] + p)  * lod;
    const uint3 v3_b = (CubeFaceDirLookup[dirLookup + 5] + p)  * lod;

    tri_b.vertices[0] = (v1_b.x & 1023) | (v1_b.y & 1023) << 10 | (v1_b.z & 1023) << 20;
    tri_b.vertices[1] = (v2_b.x & 1023) | (v2_b.y & 1023) << 10 | (v2_b.z & 1023) << 20;
    tri_b.vertices[2] = (v3_b.x & 1023) | (v3_b.y & 1023) << 10 | (v3_b.z & 1023) << 20;

    tri_b.normal = normal;
    tri_b.color = col;

    MeshBuffer.Append(tri_a);
    MeshBuffer.Append(tri_b);
}

// Main
[numthreads(8,8,8)]
void CSMain (uint3 id : SV_DispatchThreadID)
{
    const uint lod = 1 << ChunkLOD[0];
    
    const uint x = id.x * lod;
    const uint y = id.y * lod * ChunkSize[0] * ChunkSize[2];
    const uint z = id.z * lod * ChunkSize[0];
    
    const uint index = x + y + z;
    
    const int voxelIso = ChunkDataBuffer[index] & 0xF;

    if(voxelIso > 8)
    {
        const uint visibilityFlag = (ChunkDataBuffer[index] >> 4) & 63;

        const bool visXP = visibilityFlag & 1;
        const bool visXN = (visibilityFlag >> 1) & 1;
        const bool visYP = (visibilityFlag >> 2) & 1;
        const bool visYN = (visibilityFlag >> 3) & 1;
        const bool visZP = (visibilityFlag >> 4) & 1;
        const bool visZN = (visibilityFlag >> 5) & 1;

        const int packedColor = (ChunkDataBuffer[index] >> 0) & 0xFFF;

        // Unpack the colors by masking            BBBB GGGG RRRR
        const uint colorR =  packedColor & 0x00F; // 00000000[RRRR]
        const uint colorG = (packedColor & 0x0F0) >> 4; // 0000[GGGG]0000
        const uint colorB = (packedColor & 0xF00) >> 8; // [BBBB]00000000
        
        const float3 col = float3(
            colorR / 15.f,
            colorG / 15.f,
            colorB / 15.f
        );

        if(visXP)
            CreateCubeFace(0, id, col, lod);
        
        if(visXN)
            CreateCubeFace(1, id, col, lod);

        if(visYP)
            CreateCubeFace(2, id, col, lod);
        
        if(visYN)
            CreateCubeFace(3, id, col, lod);
       
        if(visZP)
            CreateCubeFace(4, id, col, lod);
        
        if(visZN)
            CreateCubeFace(5, id, col, lod);
    }
}

// Fixes incorrect count gathered by ComputeBuffer.CopyCount()
[numthreads(1,1,1)]
void FixupDrawArg()
{
    DrawArgsBuffer[0].vertexCountPerInstance *= 3;
}
